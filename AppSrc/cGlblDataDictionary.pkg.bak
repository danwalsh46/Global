//***************************************************************************
//*
//* Class:        cGlblDataDictionary
//* Package Name: cGlblDataDictionary.pkg
//*
//***************************************************************************

Use Datadict.pkg

// the use of this class assumes the following objects
// & methods to exist within the programs in which
// they are used
register_function piUser returns integer
// the following objects are created when using system logs
register_object   oWatchedFieldsArray
register_object   oRecordValuesArray
register_object   oSystemLogArray

Class cGlblDataDictionary is a DataDictionary

    // Construct_Object: Object constructor.
    procedure Construct_Object
        Forward Send Construct_Object
        // used to allow a null parent record
        Property Boolean pbAllowNullRelateState
        // used by Request_Find, Find_By_Recnum, Request_Assign
        property integer piFoundHookMsg
        // the Syslog file number
        // usage: set pilogfile to Syslog.File_number
        // this allows different applications and/or
        // files to have different file names & numbers
        property integer piLogfile         0
        // required log fields
        // log file
        property integer piLogfield1       1
        // log date
        property integer piLogfield2       2
        // log time
        property integer piLogfield3       3
        // record key1
        property integer piLogfield4       4
        // record key2
        property integer piLogfield5       5
        // record key3
        property integer piLogfield6       6
        // user
        property integer piLogfield7       7
        // log type
        property integer piLogfield8       8
        // description
        property integer piLogfield9       9
        // when TRUE this property initiates
        // System Log functionality
        Property Integer pbUsesSystemLog   False
        // when TRUE logging is only active if
        // changes are made locally
        Property Integer pbLocalOriginOnly False
        // when TRUE logging does not occur
        // on the creation of the record
        Property Integer pbNoLogOnCreate   False
        // these hold the field numbers of key fields
        // stored as part of the Syslog record
        Property Integer piKey1
        Property Integer piKey2
        Property Integer piKey3
        // the values for the key fields
        Property String  psKey1
        Property String  psKey2
        Property String  psKey3
        // Syslog records initiated from the DD must
        // be one of these three types
        property string  psLogType "U"
        // B = Business Process error
        // C = Create
        // D = Delete
        // E = Event
        // F = Failure
        // U = Update
        // V = Validate
        //
        // the following property is to store the name of
        // a currently running BPO to refine error reporting
        property string  psProcess ""
        //
    end_procedure // Construct_Object

    // Create and augment procedure and functions

    // PhoneValidation:
    Function PhoneValidation integer iField string sPhone Returns Integer
        integer iFile iLength
        string  sFPhone sAc sEx sLo
        //
        send Refind_Records
        //
        get Main_File                to iFile
        Get_Field_Value iFile iField to sFPhone
        Move (trim(sFPhone))         to sFPhone
        Move (trim(sPhone))          to sPhone
        if (sPhone = sFPhone) function_return
        //
        move (replaces("-",sPhone,"")) to sPhone
        move (length(sPhone))          to iLength
        if (iLength <= 3) begin
            set Field_Changed_Value iField to ""
            function_return
        end
        //
        //if (iLength = 7) begin
        //    move ("910" + sPhone) to sPhone
        //    move 10               to iLength
        //end
        //
        if (iLength <> 10) begin
            Send UserError "Phone Number must be 10 numbers" "Validation Error"
            function_return 1
        end
        //
        move (mid(sPhone,3,1))             to sAc
        move (mid(sPhone,3,4))             to sEx
        move (mid(sPhone,4,7))             to sLo
        move (sAc + "-" + sEx + "-" + sLo) to sPhone
        set Field_Changed_Value iField     to sPhone
    End_Function   // PhoneValidation

    Procedure Creating
        Integer iSrcFile
        //
        If (pbAllowNullRelateState(Self) and Current_Object <> OPERATION_ORIGIN) Begin
            Get Auto_Increment_Source_File to iSrcFile
            Set Auto_Increment_Source_File to 0
            Forward Send Creating
            Set Auto_Increment_Source_File to iSrcFile
        End
        Else Begin
            Forward Send Creating
        End
        //
        If (pbUsesSystemLog(Self)) Begin
            Send DoResetSystemLogArray
        End
    End_Procedure // Creating

    Procedure Backout
        Forward Send Backout
        //
        If (pbUsesSystemLog(Self)) Begin
            Send DoResetSystemLogArray
        End
    End_Procedure // Backout

    Procedure Update
        Integer bCancel
        //
        Forward Send Update
        //
        If (pbUsesSystemLog(Self)) Begin
            Move ((Current_Record(Self) = 0 and pbNoLogOnCreate(Self)) or ;
                (Operation_Origin <> Self and pbLocalOriginOnly(Self))) to bCancel
            If (bCancel) Break
            If (not(Current_Record(Self))) Set psLogType to "C"
            Else                           Set psLogType to "U"
            Send DoLoadSystemLogArray
        End
    End_Procedure // Update

    Procedure Save_Main_File
        If (not(pbAllowNullRelateState(Self) and OPERATION_ORIGIN <> Self)) Begin
            Forward Send Save_Main_File
            //
            If (pbUsesSystemLog(Self)) Begin 
                Send DoProcessSystemLogArray
            End
        End
    End_Procedure // Save_Main_File

    procedure Delete_Main_File
        forward send Delete_Main_File
        if (pbUsesSystemLog(self)) begin
            set psLogType to "D"
            send DoLoadSystemLogArray
            send DoProcessSystemLogArray
        end
    end_procedure // Delete_Main_File

    procedure DoCreateSystemLogArrays
        //
        object oWatchedFieldsArray is an Array
            // items are the field numbers
        end_object // oWatchedFieldsArray
        //
        object oRecordValuesArray is an Array
            // the "backout" values of the watched fields
        end_object // oRecordValuesArray
        //
        object oSystemLogArray is an Array
            // items in this array are fields that are watched for changes
            // and have changed, each item is a string with the field name
            // original value and new value
        end_object // oSystemLogArray
        //
    end_procedure   // DoCreateSystemLogArrays
    //
    procedure DoAddArrayValue integer hArray string sValue
        integer iItem
        get Item_Count of hArray            to iItem
        set Value      of hArray item iItem to sValue
    end_procedure // DoAddArrayValue

    procedure DoInitializeWatchedFields
        // this procedure is intended for augmentation
        // at the File subclass level in the following manner
        //integer hArray
        //send DoCreateSystemLogArrays
        //move oWatchedFieldsArray to hArray
        //// Load the watched fields array where iField is
        //// the field number from FILE.DEF
        //send DoAddArrayValue hArray iField0
        //send DoAddArrayValue hArray iField1
        //send DoAddArrayValue hArray iFieldEtc
    end_procedure // DoInitializeWatchedFields

    // DoLoadSystemLogArray:
    procedure DoLoadSystemLogArray
        integer hWatchedArray hValueArray hLogArray
        integer iMainFile iKey1 iKey2 iKey3
        integer iCount iItem iFile iField iValue
        string  sLogType sWasValue sIsValue sFieldName
        //
        get Main_File                   to iMainFile
        get piKey1                      to iKey1
        get piKey2                      to iKey2
        get piKey3                      to iKey3
        get psLogType                   to sLogType
        move oRecordValuesArray         to hValueArray
        move oWatchedFieldsArray        to hWatchedArray
        move oSystemLogArray            to hLogArray
        get Item_Count of hWatchedArray to iCount
        //
        if (sLogType <> "D") begin
            if (iKey1) begin
                // check for a related file
                get_attribute DF_FIELD_RELATED_FILE      of iMainFile iKey1 to iFile
                // if related, use the related file value
                If (iFile) Begin
                    get_attribute DF_FIELD_RELATED_FIELD of iMainFile iKey1 to iField
                End
                // otherwise use the local value
                else begin
                    move iMainFile to iFile
                    move iKey1     to iField
                end // else
                // get the value and set the string property
                get_field_value iFile iField to sIsValue
                set psKey1 to sIsValue
            end
            if (iKey2) begin
                // check for a related file
                get_attribute DF_FIELD_RELATED_FILE      of iMainFile iKey2 to iFile
                // if related, use the related file value
                If (iFile) Begin
                    get_attribute DF_FIELD_RELATED_FIELD of iMainFile iKey2 to iField
                End
                // otherwise use the local value
                else begin
                    move iMainFile to iFile
                    move iKey2     to iField
                end // else
                // get the value and set the string property
                get_field_value iFile iField to sIsValue
                set psKey2 to sIsValue
            end
            if (iKey3) begin
                // check for a related file
                get_attribute DF_FIELD_RELATED_FILE      of iMainFile iKey3 to iFile
                // if related, use the related file value
                If (iFile) Begin
                    get_attribute DF_FIELD_RELATED_FIELD of iMainFile iKey3 to iField
                End
                // otherwise use the local value
                else begin
                    move iMainFile to iFile
                    move iKey3     to iField
                end // else
                // get the value and set the string property
                get_field_value iFile iField to sIsValue
                set psKey3 to sIsValue
            end
        end // if (sLogType <> "D")
        //
        for iItem from 0 to (iCount - 1)
            get Value of hWatchedArray item iItem to iValue
            // if we are creating there are no old values
            // against which to compare
            If (sLogType <> "C") Begin
                get Value of hValueArray item iItem to sWasValue
            End
            // if we are deleting there are no new values
            if (sLogType <> "D") begin
                // check for a related file
                get_attribute DF_FIELD_RELATED_FILE      of iMainFile iValue to iFile
                // if related, use the related file value
                If (iFile) Begin
                    get_attribute DF_FIELD_RELATED_FIELD of iMainFile iValue to iField
                End
                // otherwise use the local value
                else begin
                    move iMainFile to iFile
                    move iValue    to iField
                end // else
                // get the value and set the string property
                get_field_value iFile iField to sIsValue
            end // if (sLogType <> "D")
            // either way, this happens
            if (sIsValue <> sWasValue) begin
                get_attribute DF_FIELD_NAME of iMainFile iValue to sFieldName
                move (uppercase(sFieldName))                    to sFieldName
                send DoAddArrayValue hLogArray ;
                    (trim(sFieldName) * "FROM" * trim(sWasValue) * "TO" * trim(sIsValue))
            end
        loop
    end_procedure // DoLoadSystemLogArray

    // DoResetSystemLogArray:
    procedure DoResetSystemLogArray
        integer hWatchedArray hValueArray iCount iItem iFile iField
        string  sValue
        //
        get Main_File to iFile
        // start fresh
        send Delete_Data of oSystemLogArray
        //
        if (piKey1(self)) begin
            get_field_value iFile (piKey1(self)) to sValue
            set psKey1                           to sValue
        end
        if (piKey2(self)) begin
            get_field_value iFile (piKey2(self)) to sValue
            set psKey2                           to sValue
        end
        if (piKey3(self)) begin
            get_field_value iFile (piKey3(self)) to sValue
            set psKey3                           to sValue
        end
        //
        move oWatchedFieldsArray        to hWatchedArray
        get Item_Count of hWatchedArray to iCount
        if (iCount <= 0) procedure_return
        // store the record buffer values
        // of the watched fields
        move oRecordValuesArray to hValueArray
        send Delete_Data of hValueArray
        // watched fields
        for iItem from 0 to (iCount - 1)
            get Value of hWatchedArray item iItem to iField
            get_field_value iFile iField          to sValue
            send DoAddArrayValue hValueArray sValue
        loop
    end_procedure // DoResetSystemLogArray

    Function IsTimeString Integer iHr Integer iMin Integer iSec Returns String
        string sHr sMin sSec
        //
        if (iHr  < 10) move ("0" + string(iHr )) to sHr
        else           move               iHr    to sHr
        if (iMin < 10) move ("0" + string(iMin)) to sMin
        else           move               iMin   to sMin
        if (iSec < 10) move ("0" + string(iSec)) to sSec
        else           move               iSec   to sSec
        function_return (sHr + ":" + sMin + ":" + sSec)
    end_function // IsTimeString

    function IsFormattedTime string sTime returns string
        function_return ( (left (sTime,2))   +":" ;
                         +(mid  (sTime,2,3)) +":" ;
                         +(right(sTime,2)) )
    end_function

    function IsLogDescription integer hArray integer iCount returns string
        integer iItem
        string  sArrayValue sCRLF sDescription
        //
        move (character(13) + character(10)) to sCRLF
        //
        for iItem from 0 to (iCount - 1)
            get String_Value of hArray item iItem           to sArrayValue
            move (sDescription + trim(sArrayValue) + sCRLF) to sDescription
        loop
        function_return sDescription
    end_function // IsLogDescription

    // DoProcessSystemLogArray:
    procedure DoProcessSystemLogArray
        integer hArray iFileNumber iCount iUser iHr iMin iSec iLogFile
        string  sUser sLogType sDescription sTime sKey1 sKey2 sKey3
        date    dToday
        //
        get piLogFile to iLogFile
        //
        move oSystemLogArray     to hArray
        get Item_Count of hArray to iCount
        if (iCount <= 0) procedure_return
        //
        get Main_File to iFileNumber
        //
        get psUser of ghoApplication to sUser
        move (trim(sUser))           to sUser
        get psLogType                to sLogType
        //
        If (sLogType = "D") Begin
            move "RECORD DELETED"              to sDescription
        End
        Else Begin
            get IsLogDescription hArray iCount to sDescription
        End
        //
        sysdate dToday iHr iMin iSec
        get IsTimeString iHr iMin iSec to sTime
        //
        get psKey1 to sKey1
        get psKey2 to sKey2
        get psKey3 to sKey3
        //
        clear iLogFile
        set_field_value iLogFile (piLogField1(self)) to iFileNumber
        set_field_value iLogFile (piLogField2(self)) to dToday
        set_field_value iLogFile (piLogField3(self)) to sTime
        set_field_value iLogFile (piLogField4(self)) to sKey1
        set_field_value iLogFile (piLogField5(self)) to sKey2
        set_field_value iLogFile (piLogField6(self)) to sKey3
        set_field_value iLogFile (piLogField7(self)) to sUser
        set_field_value iLogFile (piLogField8(self)) to sLogType
        set_field_value iLogFile (piLogField9(self)) to sDescription
        saverecord iLogFile
        //
        send Delete_Data of oSystemLogArray
        //
    end_procedure   // DoProcessSystemLogArray

    // this procedure is sent by the error handling process within
    // the DAW Business Processing Object
    // this procedure name cannot be changed
    procedure Log_Status string sDescription
        integer iLogFile iFileNumber iHr iMin iSec iUser
        string  sTime sUpId sUser sModuleName sProcess
        date    dToday
        //
        // UP_ID is PPMS specific
        get_environment "UP_ID"                              to sUpId
        get piUser of ghoApplication                         to iUser
        get psUser of ghoApplication                         to sUser
        if (sUpId <> "") ;
            move (trim(sUpId) * string(iUser) * trim(sUser)) to sUser
        else ;
            move (string(iUser) * trim(sUser))               to sUser
        //
        get psProgram of ghoApplication to sModuleName
        get psProcess                   to sProcess
        //
        get piLogFile to iLogFile
        get Main_File to iFileNumber
        //
        sysdate dToday iHr iMin iSec
        get IsTimeString iHr iMin iSec to sTime
        //
        clear iLogFile
        reread
        set_field_value iLogFile (piLogField1(self)) to iFileNumber
        set_field_value iLogFile (piLogField2(self)) to dToday
        set_field_value iLogFile (piLogField3(self)) to sTime
        set_field_value iLogFile (piLogField4(self)) to sModuleName
        set_field_value iLogFile (piLogField5(self)) to sProcess
        set_field_value iLogFile (piLogField7(self)) to sUser
        set_field_value iLogFile (piLogField8(self)) to "B"
        set_field_value iLogFile (piLogField9(self)) to sDescription
        saverecord iLogFile
        unlock
    end_procedure // Log_Status

    // DoDDRefresh is sent in DoRefreshView of HCSS_dbView, which is sent during Entering_Scope.
    // This procedure either redisplays the current record, or clears if the current record
    // has been deleted by another view.
    procedure DoDDRefresh
        integer iCurrentRecord iFile
        string  sCode
        //
        get Current_Record to iCurrentRecord
        if (iCurrentRecord) begin
            get Main_File to iFile
            clear iFile
            set_field_value iFile 0 to iCurrentRecord
            vfind iFile 0 EQ
            if (NOT(FOUND)) begin
                send Clear
            end
            else begin
                relate iFile
                send Request_Assign
            end
        end
    end_procedure

    Function Request_Validate_And_Save returns integer
        if (Should_Save(self) AND Request_Validate(self) = 0) send Request_Save
        function_return (Should_Save(self))
    End_Function

    Procedure Request_Find Integer iMode Integer iFile Integer iIndex
        Integer iFoundHookMsg
        //
        Forward Send Request_Find iMode iFile iIndex
        //
        If ((Found) and piFoundHookMsg(self)) Begin
            Get piFoundHookMsg to iFoundHookMsg
            Send iFoundHookMsg iFile
            Indicate Found True
        End
    End_Procedure

    Procedure Find_By_Recnum Integer iFile Integer iRecnum
        Integer iFoundHookMsg
        //
        Forward Send Find_By_Recnum iFile iRecnum
        //
        If ((Found) and piFoundHookMsg(self)) Begin
            Get piFoundHookMsg to iFoundHookMsg
            Send iFoundHookMsg iFile
            Indicate Found True
        End
    End_Procedure

//    Procedure Request_Assign Integer iFile
//        Integer iFoundHookMsg
//        //
//        Forward Send Request_Assign iFile
//        //
//        If ((Found) and piFoundHookMsg(self)) Begin
//            Get piFoundHookMsg to iFoundHookMsg
//            Send iFoundHookMsg iFile
//            Indicate Found True
//        End
//    End_Procedure

    Procedure FindByRowID Integer iFile RowID riRowId
        Integer iFoundHookMsg
        //
        Forward Send FindByRowId iFile riRowId
        //
        If ((Found) and piFoundHookMsg(self)) Begin
            Get piFoundHookMsg to iFoundHookMsg
            Send iFoundHookMsg iFile
            Indicate Found True
        End
    End_Procedure

End_Class // cGlblDataDictionary
